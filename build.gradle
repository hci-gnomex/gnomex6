buildscript {
    repositories {
        maven {
            credentials {
                username = repo_user
                password = repo_password
            }
            url repo_resolve_url
        }
        maven { url 'https://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1'
        classpath 'hci:hci-conventions:2.0.0'
    }
}

plugins {
    id 'war'
    id 'idea'
    id 'maven'
}

apply from: 'common.gradle'

import org.apache.tools.ant.filters.*

repositories {
    maven {
        credentials {
            username = repo_user
            password = repo_password
        }
        url repo_resolve_url
    }
    maven { url 'https://repo.spring.io/plugins-release' }
    mavenCentral()
}

dependencies {

    implementation platform('com.amazonaws:aws-java-sdk-bom:1.12.132')
    implementation 'com.amazonaws:aws-java-sdk-s3'

    runtime group: 'hci', name: 'hci-servlet-filter-nocache', version: '1.+'
    runtime group: 'hci', name: 'hci-servlet-filter-cors', version: '1.+'
    runtime group: 'hci', name: 'hci-servlet-filter-pushstate', version: '1.+'

    compile group: 'hci', name: 'auth-services', version: '4.+'
    compile group: 'hci', name: 'auth-persistence', version: '3.1+'
    compile group: 'hci', name: 'hci-bean-validation', version: '+'

    compile group: 'servlets.com', name: 'cos', version: '05Nov2002'
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.1'
    compile group: 'org.ostermiller', name: 'utils', version: '1.07.00'
    compile group: 'net.sf.json-lib', name: 'json-lib', version: '2.4', classifier: 'jdk15'
    compile group: 'com.itextpdf', name: 'itextpdf', version: '5.5.9'
    compile group: 'org.freemarker', name: 'freemarker', version: '2.3.20'
    compile group: 'commons-validator', name: 'commons-validator', version: '1.4.0'
    compile group: 'org.apache.lucene', name: 'lucene-core', version: '2.9.4'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group: 'jdom', name: 'jdom', version: '1.0b8'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.7'
    compile group: 'com.github.fracpete', name: 'rsync4j-all', version: '3.1.2-17'

    providedRuntime group: 'com.sun.mail', name: 'javax.mail', version: '1.5.0'

    compile group: 'jline', name: 'jline', version: '0.9.94'
    compile group: 'xom', name: 'xom', version: '1.2.5'

    //lib & libEnv
    compile fileTree(dir: 'libEnv', include: 'hci-hibernate5-utils-1.0.jar')
    compile fileTree(dir: 'lib', include: 'biojava.jar')
    compile fileTree(dir: 'lib', include: 'metrixClient-1.4.jar')
    compile fileTree(dir: 'libEnv', include: 'hci_framework.jar')
    compile fileTree(dir: 'libEnv', include: 'hci_utils.jar')
    compile fileTree(dir: 'libEnv', include: 'HCIReport.jar')
    compile fileTree(dir: 'libEnv', include: 'jdom-2.0.6.jar')
    compile fileTree(dir: 'libEnv', include: 'jaxen-1.1.6.jar')
    compile fileTree(dir: 'libEnv', include: 'poi-3.10-FINAL.jar')
    compile fileTree(dir: 'libEnv', include: 'javax.json-api-jdk8-1.1.3.jar')
    compile fileTree(dir: 'libEnv', include: 'javax.json-jdk8-1.1.3.jar')
    compile fileTree(dir: 'libEnv', include: 'hci-servlet-filter-xsrf-1.1.3.jar')
    compile fileTree(dir: 'libEnv', include: 'rsync4j-3.1.2-8.jar')
    compile fileTree(dir: 'libEnv', include: 'DuoWeb-1.3.jar')
    compile fileTree(dir: 'libEnv', include: 'processoutput4j-0.0.7.jar')
    compile fileTree(dir: 'libEnv', include: 'argparse4j-0.6.0.jar')

//    compile fileTree(dir: 'libEnv', include: 'tempus-persistence.jar')

    //Realm stuff
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
    providedCompile group: 'log4j', name: 'log4j', version: '1.2.12'
    providedCompile group: 'org.apache.tomcat', name: 'tomcat-jdbc', version: '9.0.0.M10'
    providedCompile group: 'org.apache.tomcat', name: 'tomcat-catalina', version: '7.0.68'

    //Subprojects
    providedCompile project(':gnomex_crypt')
    providedCompile project(':gnomex_tomcat_support')

    //Tomcat
    compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.2.Final'
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: '5.0.10.Final'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.+'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'

    // JAX-RS Related
    compile group: 'org.jboss.resteasy', name: 'resteasy-jaxrs', version: '3.0.19.Final'
    compile group: 'org.jboss.resteasy', name: 'resteasy-jackson2-provider', version: '3.0.19.Final'
    compile group: 'org.jboss.resteasy', name: 'resteasy-client', version: '3.0.19.Final'
    compile group: 'org.jboss.resteasy', name: 'resteasy-servlet-initializer', version: '3.0.19.Final'
    compile group: 'org.jboss.resteasy', name: 'resteasy-cdi', version: '3.0.19.Final'

    // CDI Related
    providedCompile group: 'javax.ejb', name: 'javax.ejb-api', version: '3.2'
    providedCompile group: 'javax.enterprise', name: 'cdi-api', version: '1.2'
    runtime group: 'org.jboss.weld.servlet', name: 'weld-servlet', version: '2.4.1.Final'
}

// Main build tasks are named starting with "gnomex"
task gnomexWindowsWDS(type: GradleBuild) {
    tasks = ['adjustShiroForWindows', 'buildWar', 'copyAngularQuick', 'copyWar']
}

task gnomexWindowsTomcat(type: GradleBuild) {
    tasks = ['adjustShiroForWindows', 'buildWar', 'copyAngular', 'copyWar']
}

task gnomexLinuxTomcat(type: GradleBuild) {
    tasks = ['adjustShiroForLinux', 'buildWar', 'copyAngular', 'copyWar']
}

task gnomexOpensource(type: GradleBuild) {
    tasks = ['adjustShiroForLinux', 'buildWarOpensource', 'copyAngular', 'copyWar', 'createOpenSourceZip']
}

task adjustShiroForLinux() {
    doLast {
        copy {
            from "src/main/webapp/WEB-INF"
            include "shiro.ini"
            filter { line -> line.replace(" C:/properties", " /properties")}
            into "src/main/webapp/WEB-INF/temp"
        }
        copy {
            from "src/main/webapp/WEB-INF/temp"
            include "shiro.ini"
            into "src/main/webapp/WEB-INF"
        }
        delete {
            delete "src/main/webapp/WEB-INF/temp"
        }
    }
}

task adjustShiroForWindows() {
    doLast {
        copy {
            from "src/main/webapp/WEB-INF"
            include "shiro.ini"
            filter { line -> line.replace(" /properties", " C:/properties")}
            into "src/main/webapp/WEB-INF/temp"
        }
        copy {
            from "src/main/webapp/WEB-INF/temp"
            include "shiro.ini"
            into "src/main/webapp/WEB-INF"
        }
        delete {
            delete "src/main/webapp/WEB-INF/temp"
        }
    }
}

task buildWar(type: GradleBuild) {
    mustRunAfter(adjustShiroForWindows, adjustShiroForLinux)
    tasks = ['clean', 'cleanTomcat', 'makeSQLServer', 'jar', 'clientJar', 'copyScripts', 'copyTomcatJars', 'war']
}

task buildWarOpensource(type: GradleBuild) {
    mustRunAfter(adjustShiroForWindows, adjustShiroForLinux)
    tasks = ['clean', 'cleanTomcat', 'makeMySQL', 'jar', 'clientJar', 'copyScripts', 'copyTomcatJars', 'opensourceWar']
}

task cleanTomcat(type: Delete) {
    delete "${gnomexServerHome}/${gnomexDeployDir}/gnomex"
}

static void replaceString(String fileName, String fromString, String toString) {
    File file = new File(fileName)
    String fileText = file.text
    fileText = fileText.replace(fromString, toString)
    new File(fileName).write(fileText)
}

task makeMySQL() {
    replaceString("${rescDir}/hibernate.tomcat.cfg.xml", "SQLServerDialect", "MySQLDialect")
}

task makeSQLServer() {
    replaceString("${rescDir}/hibernate.tomcat.cfg.xml", "MySQLDialect", "SQLServerDialect")
}

jar {
    archiveFileName = jarName
    manifest {
        attributes(
                'Class-Path': 'log4j.jar'
        )
        attributes([
                'Built-By'              : repo_user,
                'Implementation-Version': "${gnomex_version} (${formattedDate})"
        ], 'gnomex')
    }
}

task clientJar(type: Jar) {
    from buildDir.getAbsolutePath() + "/classes/main/"
    include "hci/gnomex/httpclient/*.class"
    archiveFileName = clientJarName

    manifest {
        attributes([
                'Built-By'              : repo_user,
                'Implementation-Version': gnomex_version
        ], 'gnomexClient')
    }
}

task copyScripts(type: Copy) {
    from 'scripts'
    include '*.*'
    into buildDir.getAbsolutePath() + "/scripts"
    filter(FixCrLfFilter)
}

task copyTomcatJars(type: Copy) {
    dependsOn ':gnomex_tomcat_support:jar', ':gnomex_crypt:jar'
    from 'gnomex_tomcat_support/build/libs'
    from 'gnomex_crypt/build/libs'
    into "${gnomexServerHome}/lib"
}

war {
    archiveFileName = warNameTemp
    classpath = classpath - sourceSets.main.output

    manifest {
        attributes(
                'Built-By': repo_user,
                'Implementation-Version': "${gnomex_version} (${formattedDate})",
                'Dependencies': 'org.dom4j, org.apache.log4j'
        )
    }

    rootSpec.exclude("**/tomcat-annotations-api-7.0.68.jar")
    rootSpec.exclude("**/tomcat-api-7.0.68.jar")
    rootSpec.exclude("**/tomcat-catalina-7.0.68.jar")
    rootSpec.exclude("**/tomcat-servlet-api-7.0.68.jar")
    rootSpec.exclude("**/tomcat-util-7.0.68.jar")

    from(buildDir.getAbsolutePath() + "/libs") {
        include "${jarName}"
        include "${clientJarName}"
        into 'WEB-INF/lib'
    }

    from("$projectDir/META-INF") {
        include "context.xml"
        into 'META-INF'
    }

    from("$buildDir/scripts") {
        into "scripts"
    }

    from(rescDir) {
        include '*.*'
        into 'WEB-INF/classes'
    }

    from("${rescDir}/hci/gnomex") {
        include 'Dictionaries.xml'
        into 'WEB-INF/classes'
    }

    from("$projectDir") {
        include "UCSCExecutables/**"
    }

    from("$projectDir/src/main/webapp") {
        include "getXML.jsp"
        include "getHTML.jsp"
        into 'WEB-INF/classes'
    }
}

task opensourceWar(type: War) {
    archiveFileName = warNameTemp

    manifest {
        attributes(
                'Built-By': repo_user,
                'Implementation-Version': "${gnomex_version} (${formattedDate})"
        )
    }

    // Use UNIX eol convention
    from("${buildDir}/scripts") {
        include "*.sh"
        filter(FixCrLfFilter.class, eol: FixCrLfFilter.CrLf.newInstance("lf"))
        into 'scripts'
    }

    from(buildDir.getAbsolutePath() + "/libs") {
        include "${jarName}"
        include "${clientJarName}"
        into 'WEB-INF/lib'
    }

    from("$projectDir/META-INF") {
        include "context_opensource.xml"
        into 'META-INF'
        rename('context_opensource.xml', 'context.xml')
    }

    from(rescDir) {
        include '*.*'
        into 'WEB-INF/classes'
    }

    from("${rescDir}/hci/gnomex") {
        include 'Dictionaries.xml'
        into 'WEB-INF/classes'
    }

    from("$projectDir") {
        include "UCSCExecutables/**"
    }

    from("$projectDir/src/main/webapp") {
        include "getXML.jsp"
        include "getHTML.jsp"
        into 'WEB-INF/classes'
    }
}

task copyAngular(type: Zip) {
    mustRunAfter(buildWar, buildWarOpensource)
    dependsOn ':gnomex_ng:buildWebpackDevTomcat'
    from zipTree("build/libs/${warNameTemp}")
    from("gnomex_ng/dist") {
        include "**/*"
    }
    exclude 'WEB-INF/lib/hibernate-entitymanager-5.0.10.Final.jar'
    exclude 'WEB-INF/lib/hci_framework.jar'
    exclude 'WEB-INF/lib/javaee-api-7.0.jar'
    exclude 'WEB-INF/lib/javax.mail-1.5.0.jar'
    destinationDir(file("build/libs"))
    archiveName("${warName}")
}

task copyAngularQuick(type: Zip) {
    mustRunAfter(buildWar, buildWarOpensource)
    from zipTree("build/libs/${warNameTemp}")
    from("gnomex_ng/src") {
        include "assets/**"
        include "favicon.ico"
        include "index.html"
    }
    exclude 'WEB-INF/lib/hibernate-entitymanager-5.0.10.Final.jar'
    exclude 'WEB-INF/lib/hci_framework.jar'
    exclude 'WEB-INF/lib/javaee-api-7.0.jar'
    exclude 'WEB-INF/lib/javax.mail-1.5.0.jar'
    destinationDir(file("build/libs"))
    archiveName("${warName}")
}

task copyWar(type: Copy) {
    mustRunAfter(copyAngular, copyAngularQuick)
    from ("build/libs") {
        include "${warName}"
    }
    into "${gnomexServerHome}/${gnomexDeployDir}"
}

task createOpenSourceZip(type: Zip) {
    mustRunAfter(copyAngular, copyAngularQuick)

    archiveFileName = openSourceZipName

    from("$projectDir") {
        include "README_INSTALL.txt"
        filter(FixCrLfFilter.class, eol: FixCrLfFilter.CrLf.newInstance("lf"))
        into openSourceName
    }

    from("$projectDir") {
        include "README_UPGRADE.txt"
        into openSourceName
    }

    from("$projectDir") {
        include "LICENSE.txt"
        into openSourceName
    }

    from("$projectDir/config/tomcat_config") {
        include "*.jar"
        into "${openSourceName}/server/tomcat/lib"
    }

    from("$projectDir/scripts") {
        include "*.*"
        into "${openSourceName}/scripts"
    }

    from(buildDir.getAbsolutePath() + "/libs") {
        include "${warName}"
        include "${clientJarName}"
        into "${openSourceName}/gnomex/build"
    }

    from('gnomex_tomcat_support/build/libs') {
        into "${openSourceName}/gnomex/build"
    }

    from('gnomex_crypt/build/libs') {
        into "${openSourceName}/gnomex/build"
    }

    from("$projectDir/doc") {
        include "*/**"
        into "${openSourceName}/gnomex/doc"
    }

    from("$projectDir/sql") {
        include "*/**"
        into "${openSourceName}/gnomex/sql"
    }
}
